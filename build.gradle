group 'niclabs'
version '1.0'

apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'TestInConsole'

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

// Create a single .jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': 'TestInConsole'
    }
    baseName = 'dcnet-participant'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

// Create a javadoc
task generateAPIDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.runtimeClasspath
    destinationDir = file("${buildDir}/docs/myjavadoc")
    options.header = "<script type=\"text/javascript\" src=\"http://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-AMS-MML_HTMLorMML\"></script>";
}

run {
    standardInput = System.in
    classpath = sourceSets.main.runtimeClasspath
    if ( project.hasProperty("appArgs") ) {
        args Eval.me(appArgs)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.zeromq:jeromq:0.3.5'
    compile 'com.google.code.gson:gson:2.5'
    compile group: 'org.apache.maven.plugins', name: 'maven-javadoc-plugin', version: '2.10.4'
}